// Adopted from: https://etherscan.io/token/0x95aD61b0a150d79219dCF64E1E6Cc01f0B64C4cE#code

// parameters
.decl *owner(p: address)

// views
.decl *totalSupply(n: uint)
.decl balanceOf(p: address, n: uint)[0]
.decl allowance(p: address, s: address, n:uint)[0,1]

// constructor
.decl constructor(name: uint, symbol: uint, decimals: uint, totalSupply: uint, feeReceiver: address, tokenOwnerAddress: address)



// Transactions
.decl mint(p: address, amount: uint)
.decl recv_mint(p: address, amount: uint)
.decl burn(p: address, amount: uint)
.decl recv_burn(amount: uint)
.decl transfer(from: address, to: address, amount: uint)
.decl recv_transfer(to: address, amount: uint)
.decl recv_transferFrom(from: address, to: address, amount: uint)
.decl transferFrom(from: address, to: address, spender: address, amount: uint)
.decl recv_increaseAllowance(s: address, n:uint)
.decl increaseAllowance(p: address, s: address, n:uint)
.decl recv_decreaseAllowance(s: address, n:uint)
.decl decreaseAllowance(p: address, s: address, n:uint)
.decl recv_approve(s: address, n:uint)

// Interfaces
.public recv_burn
.public recv_transfer
.public recv_approve
.public recv_transferFrom
.public recv_increaseAllowance
.public recv_decreaseAllowance
.public balanceOf(1)
.public totalSupply(0)
.public allowance(2)

// Rules
owner(s) :- constructor(_,_,_,_,_,_), msgSender(s).
totalSupply(n) :- constructor(_,_,_,n,_,_).

mint(p,n) :- recv_mint(p,n), msgSender(s), owner(s), p!=0.
balanceOf(p,n) :- constructor(_,_,_,n,_,p).
send(p,n) :- constructor(_,_,_,_,p,_), msgValue(n).

burn(s,n) :- recv_burn(n), msgSender(s), s!=0, balanceOf(s,m), n<=m.
transfer(s,r,n) :- recv_transfer(r,n), msgSender(s), balanceOf(s,m), n<=m.

.decl totalMint(p: address, n: uint)[0]
.decl totalBurn(p: address, n: uint)[0]
.decl totalOut(p: address, n: uint)[0]
.decl totalIn(p: address, n: uint)[0]
totalOut(p,s) :- transfer(p,_,_), s = sum n: transfer(p,_,n).
totalIn(p,s) :- transfer(_,p,_), s = sum n: transfer(_,p,n).
totalMint(p,s) :- mint(p,_), s = sum n: mint(p,n).
totalBurn(p,s) :- burn(p,_), s = sum n: burn(p,n).
balanceOf(p,s) :- totalMint(p,n), totalBurn(p,m), totalOut(p,o), totalIn(p,i), s:=n+i-m-o.

.decl *allMint(n: uint)
.decl *allBurn(n: uint)
allMint(s) :- s = sum n: mint(_,n).
allBurn(s) :- s = sum n: burn(_,n).
totalSupply(n) :- allMint(m), allBurn(b), n := m - b.

// New rules
transferFrom(o,r,s,n) :- recv_transferFrom(o,r,n), balanceOf(o,m), m>=n, msgSender(s), allowance(o,s,k), k>=n.
transfer(o,r,n) :- transferFrom(o,r,_,n).

increaseAllowance(o,s,d) :- recv_approve(s,n), msgSender(o), allowance(o,s,m), n>=m, d:=n-m.
decreaseAllowance(o,s,d) :- recv_approve(s,n), msgSender(o), allowance(o,s,m), n<m, d:=m-n.

.decl increaseAllowanceTotal(o:address, s:address, m:uint)[0,1]
.decl decreaseAllowanceTotal(o:address, s:address, m:uint)[0,1]
.decl spentTotal(o:address, s:address, m:uint)[0,1]
increaseAllowance(o,s,n) :- recv_increaseAllowance(s,n), msgSender(o).
decreaseAllowance(o,s,n) :- recv_decreaseAllowance(s,n), msgSender(o).
increaseAllowanceTotal(o,s,m) :- increaseAllowance(o,s,_), m = sum n: increaseAllowance(o,s,n).
decreaseAllowanceTotal(o,s,m) :- decreaseAllowance(o,s,_), m = sum n: decreaseAllowance(o,s,n).
spentTotal(o,s,m) :- transferFrom(o,_,s,_), m = sum n: transferFrom(o,_,s,n).
allowance(o,s,n) :- increaseAllowanceTotal(o,s,m), decreaseAllowanceTotal(o,s,d), spentTotal(o,s,l), n := m-l-d.


.decl recv_burnFrom(p:address, n:uint)
.decl burnFrom(p:address, from:address, n:uint)

burnFrom(s,p,n) :- recv_burnFrom(p,n), msgSender(s), p!=0, balanceOf(p,m), m >= n, allowance(p,s,k), k >= m.
transferFrom(s,p,0,n) :- burnFrom(s,p,n).
burn(p,n) :- burnFrom(_,p,n).

// Properties
.decl *totalBalances(m: uint)
.decl *unequalBalance(s: uint, n: uint)
.violation unequalBalance
totalBalances(n) :- constructor(_,_,_,n,_,_).
totalBalances(s) :- s = sum n: balanceOf(_,n).
unequalBalance(s,n) :- totalBalances(s), totalSupply(n), s!=n.
