// parameters
.decl *owner(p: address)

// views
.decl *totalSupply(n: int)
.decl balanceOf(p: address, n: int)[0]
.decl constructor()

// Transactions
.decl mint(p: address, amount: int)
.decl recv_mint(p: address, amount: int)
.decl burn(p: address, amount: int)
.decl recv_burn(p: address, amount: int)
.decl transfer(from: address, to: address, amount: int)
.decl recv_transfer(from: address, to: address, amount: int)

// Interfaces
.interface recv_mint
.interface recv_burn
.interface recv_transfer
.interface balanceOf(1)
.interface totalSupply(0)

// Rules
owner(s) :- constructor(), msgSender(s).

mint(p,n) :- recv_mint(p,n), msgSender(s), owner(s), n>0.
burn(p,n) :- recv_burn(p,n), msgSender(s), owner(s), balanceOf(p,m), n<=m.
transfer(s,r,n) :- recv_transfer(s,r,n), balanceOf(s,m), m>=n.

.decl totalMint(p: address, n: int)[0]
.decl totalBurn(p: address, n: int)[0]
.decl totalOut(p: address, n: int)[0]
.decl totalIn(p: address, n: int)[0]
totalOut(p,s) :- transfer(p,_,_), s = sum n: transfer(p,_,n).
totalIn(p,s) :- transfer(_,p,_), s = sum n: transfer(_,p,n).
totalMint(p,s) :- mint(p,_), s = sum n: mint(p,n).
totalBurn(p,s) :- burn(p,_), s = sum n: burn(p,n).
balanceOf(p,s) :- totalMint(p,n), totalBurn(p,m), totalOut(p,o), totalIn(p,i), s:=n+i-m-o.

.decl *allMint(n: int)
.decl *allBurn(n: int)
allMint(s) :- s = sum n: mint(_,n).
allBurn(s) :- s = sum n: burn(_,n).
totalSupply(n) :- allMint(m), allBurn(b), n := m - b.

// Properties
.decl *totalBalances(m: int)
.decl *unequalBalance(s: int, n: int, valid:bool)

.violation unequalBalance

totalBalances(s) :- s = sum n: balanceOf(_,n).
unequalBalance(s,n,true) :- totalBalances(s), totalSupply(n), s!=n.
