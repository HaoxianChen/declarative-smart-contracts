// Adopted form Etherscan: https://etherscan.io/token/0xB8c77482e45F1F44dE1745F52C74426C631bDD52#code

// parameters
.decl *owner(p: address)

// views
.decl *totalSupply(n: uint)
.decl balanceOf(p: address, n: uint)[0]
.decl constructor(n: uint)

// New views
.decl allowance(p: address, s: address, n:uint)[0,1]
.decl *paused(b:bool)
.decl isPauser(p: address, b: bool)[0]

// Transactions
.decl mint(p: address, amount: uint)
.decl recv_mint(p: address, amount: uint)
.decl burn(p: address, amount: uint)
.decl recv_burn(p: address, amount: uint)
.decl transfer(from: address, to: address, amount: uint)
.decl recv_transfer(to: address, amount: uint)

// New transactions
.decl recv_transferFrom(from: address, to: address, amount: uint)
.decl increaseAllowance(p: address, s: address, n:uint)
.decl recv_approve(s: address, n:uint)

.decl addPauser(p: address, b: bool)[0]
.decl renouncePauser(p: address, b: bool)[0]
.decl recv_addPauser(p:address)
.decl recv_renouncePauser()

addPauser(p,true):- recv_addPauser(p), msgSender(s), isPauser(s,true).
renouncePauser(s,false) :- recv_renouncePauser(), msgSender(s), isPauser(s,true).

.decl *pause(b:bool)
.decl *unpause(b:bool)
.decl recv_pause()
.decl recv_unpause()
pause(true) :- recv_pause(), msgSender(s), isPauser(s,true), paused(false).
unpause(false) :- recv_unpause(), msgSender(s), isPauser(s,true), paused(true).

.public recv_pause
.public recv_unpause
.public recv_addPauser
.public recv_renouncePauser

// Interfaces
.public recv_mint
.public recv_burn
.public recv_transfer
.public balanceOf(1)
.public totalSupply(0)

// New interfaces
.public recv_approve
.public recv_transferFrom
.public allowance(2)

.decl totalMint(p: address, n: uint)[0]
.decl totalBurn(p: address, n: uint)[0]
.decl totalOut(p: address, n: uint)[0]
.decl totalIn(p: address, n: uint)[0]

.decl *allMint(n: uint)
.decl *allBurn(n: uint)

// Rules
owner(s) :- constructor(_), msgSender(s).
totalSupply(n) :- constructor(n).
paused(false) :- constructor(_).
isPauser(s,true) :- constructor(_),msgSender(s).
balanceOf(s,n) :- constructor(n),msgSender(s).
totalMint(s,n) :- constructor(n),msgSender(s).
allMint(n) :- constructor(n).

mint(p,n) :- recv_mint(p,n), msgSender(s), owner(s), p!=0, paused(false).
burn(p,n) :- recv_burn(p,n), msgSender(s), owner(s), p!=0 ,balanceOf(p,m), n<=m,
              paused(false).
transfer(s,r,n) :- recv_transfer(r,n), msgSender(s), balanceOf(s,m), n<=m,
              paused(false).

totalOut(p,s) :- transfer(p,_,_), s = sum n: transfer(p,_,n).
totalIn(p,s) :- transfer(_,p,_), s = sum n: transfer(_,p,n).
totalMint(p,s) :- mint(p,_), s = sum n: mint(p,n).
totalBurn(p,s) :- burn(p,_), s = sum n: burn(p,n).
balanceOf(p,s) :- totalMint(p,n), totalBurn(p,m), totalOut(p,o), totalIn(p,i), s:=n+i-m-o.

allMint(s) :- s = sum n: mint(_,n).
allBurn(s) :- s = sum n: burn(_,n).
totalSupply(n) :- allMint(m), allBurn(b), n := m - b.

// New rules
.decl transferFrom(from: address, to: address, spender: address, amount: uint)
transferFrom(o,r,s,n) :- recv_transferFrom(o,r,n),
                    balanceOf(o,m), m>=n,
                    msgSender(s), allowance(o,s,k), k>=n,
                    paused(false).
transfer(o,r,n) :- transferFrom(o,r,_,n).

.decl recv_increaseAllowance(p:address, n:uint)
.decl recv_decreaseAllowance(p:address, n:uint)
.decl decreaseAllowance(p: address, s: address, n:uint)
.public recv_increaseAllowance
.public recv_decreaseAllowance

increaseAllowance(o,s,d) :- recv_approve(s,n), msgSender(o), allowance(o,s,m), d:=n-m.
increaseAllowance(o,s,n) :- recv_increaseAllowance(s,n), msgSender(o).
decreaseAllowance(o,s,n) :- recv_decreaseAllowance(s,n), msgSender(o), allowance(o,s,m), m>=n.

.decl allowanceTotal(o:address, s:address, m:uint)[0,1]
.decl decreaseAllowanceTotal(o:address, s:address, m:uint)[0,1]
.decl spentTotal(o:address, s:address, m:uint)[0,1]
allowanceTotal(o,s,m) :- increaseAllowance(o,s,_), m = sum n: increaseAllowance(o,s,n).
decreaseAllowanceTotal(o,s,m) :- decreaseAllowance(o,s,_), m = sum n: decreaseAllowance(o,s,n).
spentTotal(o,s,m) :- transferFrom(o,_,s,_), m = sum n: transferFrom(o,_,s,n).

allowance(o,s,n) :- allowanceTotal(o,s,m), spentTotal(o,s,l), decreaseAllowanceTotal(o,s,d), n := m-l-d.

paused(b) :- pause(b).
paused(b) :- unpause(b).
isPauser(p,b) :- addPauser(p,b).
isPauser(p,b) :- renouncePauser(p,b).

// Properties
.decl *totalBalances(m: uint)
.decl *unequalBalance(s: uint, n: uint)

.violation unequalBalance

totalBalances(n) :- constructor(n).
totalBalances(s) :- s = sum n: balanceOf(_,n).
unequalBalance(s,n) :- totalBalances(s), totalSupply(n), s!=n.
